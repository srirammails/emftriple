-- @atlcompiler atl2006

library RDFGenHelpers;

helper context ecore!EClass def: getGraph(): String = 
	let annotation: ecore!EAnnotation = 
		self.eAnnotations->any(e | e.source = 'Graph' or e.source = 'PutGraph')
	in if (annotation.oclIsUndefined()) then
		thisModule.Graph
	else 
		if annotation.details.containsKey('uri') then
			annotation.details.get('uri')
		else thisModule.Graph endif
	endif;

helper context ecore!EClass def: getID(): ecore!EStructuralFeature =
	let features: Set(ecore!EStructuralFeature) = 
		self.getEAllStructuralFeatures()
	in 
		let annotated: ecore!EStructuralFeature = 
				features->any(e | e.hasIdAnnotation())
		in 
		if (not annotated.oclIsUndefined()) then
			annotated
		else 
			let 
				id: ecore!EStructuralFeature = features->any(e | e.isID()) 
			in 
			if (not id.oclIsUndefined()) then 
				id
			else features->any(e | e.name = 'name') endif
		endif;

helper context ecore!EAnnotation def: getIdBase(class: ecore!EClass): String =
	if (self.details.containsKey('base')) then
		self.details.get('base').regexReplaceAll('\\[Class\\]', class.getName().toLower())
	else self.getURI() endif;
		
helper context ecore!EStructuralFeature def: hasIdAnnotation(): Boolean =
	self.eAnnotations->exists(e | e.source = 'Id');

helper context ecore!EStructuralFeature def: getIdAnnotation(): ecore!EAnnotation =
	self.eAnnotations->any(e | e.source = 'Id');


